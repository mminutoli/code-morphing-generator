%skeleton "lalr1.cc"
%require "2.5"
%language "c++"
%debug
%defines
%locations
%define api.namespace { IA }
%define parser_class_name { AlternativesParser }


%code requires {

#include <sstream>
#include <string>
#include <iostream>

namespace IA {
  class AlternativesScanner;
  class AlternativesDriver;
}

}

%lex-param { AlternativesScanner & scanner }
%parse-param { AlternativesScanner & scanner }

%lex-param { AltenativesDriver & driver }
%parse-param { AlternativesDriver & driver }

%code {

#include <AlternativesDriver.hpp>

static int yylex(IA::AlternativesParser::semantic_type * yylval,
                 IA::AlternativesParser::location_type *,
                 IA::AlternativesScanner & scanner,
                 IA::AlternativesDriver & driver);

}

%start alternatives

%union {
  std::string * sval;
  unsigned int eval;
}


%token  END 0
%token  LBRACE RBRACE LPAR RPAR LSQUARE RSQUARE
%token  <sval> OP
%token  <sval> BINOP
%token  <sval> REG CONST RAND
%token  <eval> NUMBER
%token  ASSIGN

%token  <eval> LNOT BNOT
%token  <eval> MULT DIV
%token  <eval> PLUS MINUS
%token  <eval> XOR AND OR

%type <sval> argument
%type <eval> expression
%type <eval> term
%type <eval> factor
%type <eval> const_argument

%%

alternatives : alternative_record END
{
  driver.buildAlternativesSpecialization();
}
             | alternative_record alternatives

alternative_record : instruction_replaced LBRACE instruction_alternatives RBRACE
{
  driver.addAlternative();
}

instruction_replaced : REG ASSIGN BINOP REG REG
{
  driver.currentInstruction(*$3);
  driver.regReg();
  driver.setInputs(*$4, *$5);
}
                     | REG ASSIGN BINOP REG CONST
{
  driver.currentInstruction(*$3);
  driver.regConst();
  driver.setInputs(*$4, *$5);
}


instruction_alternatives : instruction_alternative
                         | instruction_alternative instruction_alternatives

instruction_alternative : LSQUARE
{
  if (driver.isRegReg())
    driver.openRegRegAlternativeBuilder();
  else
    driver.openRegConstAlternativeBuilder();
}
                          instruction_list
                          RSQUARE { driver.closeAlternativeBuilder(); }

instruction_list : instruction
                 | instruction instruction_list

instruction : REG ASSIGN BINOP argument argument
{
  driver.emitInstruction(*$1, *$3, *$4, *$5);
}
            | REG ASSIGN OP argument
{
  driver.emitInstruction(*$1, *$3, *$4);
}

argument : REG
         | CONST
         | RAND { driver.handleRandomArgs(*$1); }
         | LPAR expression RPAR { $$ = driver.handleGenTimeConstant($2); }

const_argument : RAND { $$ = driver.handleRandomArgs(*$1); }
               | NUMBER

expression : expression XOR term { $$ = $1 ^ $3; }
           | expression AND term { $$ = $1 & $3; }
           | expression OR term { $$ = $1 | $3; }
           | term

term : term PLUS factor { $$ = $1 + $3; }
     | term MINUS factor { $$ = $1 - $3; }
     | factor

factor : const_argument
       | LNOT const_argument { $$ = !$2; }
       | BNOT const_argument { $$ = ~$2; }
       | LPAR expression RPAR  { $$ = $2; }

%%

void IA::AlternativesParser::error(const IA::AlternativesParser::location_type &l,
                                   const std::string &err_message)
{
  std::cerr << "Error: " << err_message << "\n";
}

#include <AlternativesScanner.hpp>
static int yylex(IA::AlternativesParser::semantic_type * yylval,
                 IA::AlternativesParser::location_type *,
                 IA::AlternativesScanner & scanner,
                 IA::AlternativesDriver & driver)
{
  return scanner.yylex(yylval);
}
